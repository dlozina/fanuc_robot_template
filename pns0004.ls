/PROG  PNS0004
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "TO M1 FROM H2";
PROG_SIZE	= 2210;
CREATE		= DATE 19-04-17  TIME 08:31:40;
MODIFIED	= DATE 19-04-18  TIME 15:34:24;
FILE_NAME	= PNS0003;
VERSION		= 0;
LINE_COUNT	= 139;
MEMORY_SIZE	= 2594;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/MN
   1:  !GRIJAC 2 NA STROJ 1 ;
   2:   ;
   3:  OVERRIDE=30% ;
   4:   ;
   5:  CALL CLR_SCR    ;
   6:  GO[1]=R[100:Job Number] ;
   7:   ;
   8:  CALL H2_STOP    ;
   9:   ;
  10:  UFRAME_NUM=12 ;
  11:  UTOOL_NUM=1 ;
  12:   ;
  13:J PR[38:V_WELDING_1 ] 100% FINE    ;
  14:   ;
  15:J PR[4:V_H1H2] 100% FINE    ;
  16:   ;
  17:J PR[5:V_MID_H1H2] 100% FINE    ;
  18:   ;
  19:J PR[34:V_HEATER2] 100% FINE    ;
  20:   ;
  21:  WAIT   6.00(sec) ;
  22:   ;
  23:  WAIT DI[14:PermissSpace7]=ON    ;
  24:   ;
  25:  UFRAME_NUM=7 ;
  26:  UTOOL_NUM=1 ;
  27:   ;
  28:  R[6:HEATER2 Z OFFS]=R[113:HEAT2 HEIGHT]+20    ;
  29:  CALL PR(96,0,R[114:HEAT2 Y OFFS],R[6:HEATER2 Z OFFS]) ;
  30:   ;
  31:J PR[35:HEATER_2_REF] 100% CNT20 Offset,PR[96:OFFSET H2]    ;
  32:   ;
  33:  R[6:HEATER2 Z OFFS]=R[112:HEAT2 HANDLING]+4    ;
  34:   ;
  35:  CALL PR(96,0,R[114:HEAT2 Y OFFS],R[6:HEATER2 Z OFFS]) ;
  36:   ;
  37:L PR[35:HEATER_2_REF] 200mm/sec FINE Offset,PR[96:OFFSET H2]    ;
  38:   ;
  39:  CALL GRP_CLS    ;
  40:   ;
  41:  DO[52:PICK AND PLACE PLS]=PULSE,1.0sec ;
  42:   ;
  43:  R[40:Handling height]=R[112:HEAT2 HANDLING]    ;
  44:  R[41:WP Height]=R[113:HEAT2 HEIGHT]    ;
  45:   ;
  46:  R[114:HEAT2 Y OFFS]=0    ;
  47:  R[112:HEAT2 HANDLING]=0    ;
  48:  R[113:HEAT2 HEIGHT]=0    ;
  49:   ;
  50:  R[6:HEATER2 Z OFFS]=R[40:Handling height]+50    ;
  51:   ;
  52:  CALL PR(96,0,R[5:HEATER2 Y OFFS],R[6:HEATER2 Z OFFS]) ;
  53:   ;
  54:L PR[35:HEATER_2_REF] 100mm/sec FINE Offset,PR[96:OFFSET H2]    ;
  55:   ;
  56:  UFRAME_NUM=12 ;
  57:  UTOOL_NUM=1 ;
  58:   ;
  59:J PR[34:V_HEATER2] 30% CNT20    ;
  60:   ;
  61:J PR[5:V_MID_H1H2] 30% CNT20    ;
  62:   ;
  63:J PR[4:V_H1H2] 100% CNT20    ;
  64:   ;
  65:  !TO WELDING 1 ;
  66:  UFRAME_NUM=12 ;
  67:  UTOOL_NUM=1 ;
  68:   ;
  69:J PR[38:V_WELDING_1 ] 30% CNT20    ;
  70:  WAIT DI[12:PermissSpace5]=ON    ;
  71:   ;
  72:  UFRAME_NUM=12 ;
  73:  UTOOL_NUM=1 ;
  74:   ;
  75:J PR[40:IN_WELDING_1] 30% CNT20    ;
  76:   ;
  77:  CALL PR(97,0,0,(-250)) ;
  78:   ;
  79:J PR[40:IN_WELDING_1] 30% CNT20 Tool_Offset,PR[97:OFFSET W1]    ;
  80:   ;
  81:  UFRAME_NUM=5 ;
  82:  UTOOL_NUM=1 ;
  83:   ;
  84:  R[64:W1 CHUCK HEIGHT]=126-GI[24:M1_Chuck_Height]    ;
  85:  R[65:W1 CHUCK DEPTH]=GI[23:M1_Chuck_Depth]    ;
  86:   ;
  87:  R[17:WELDING1 X OFFS]=R[42:Pick TCP Offs]+16    ;
  88:   ;
  89:  !CORRECTION X OFFS ;
  90:  R[1:CHECK SIGN GI]=(GI[25:M1_Chuck_XOffs]-(GI[25:M1_Chuck_XOffs] DIV 32786)*65536) ;
  91:   ;
  92:  R[17:WELDING1 X OFFS]=R[17:WELDING1 X OFFS]+20+R[1:CHECK SIGN GI]    ;
  93:   ;
  94:  R[18:WELDING1 Y OFFS]=GI[26:M1_Chuck_YOffs ]    ;
  95:   ;
  96:  R[19:WELDING1 Z OFFS]=R[40:Handling height]+R[64:W1 CHUCK HEIGHT]+20    ;
  97:   ;
  98:  CALL PR(97,R[17:WELDING1 X OFFS],R[18:WELDING1 Y OFFS],R[19:WELDING1 Z OFFS]) ;
  99:  CALL CHECK_SIGN(97,2) ;
 100:   ;
 101:J PR[39:WELDING_1_REF] 30% CNT20 Offset,PR[97:OFFSET W1]    ;
 102:   ;
 103:  R[19:WELDING1 Z OFFS]=R[40:Handling height]-R[65:W1 CHUCK DEPTH]    ;
 104:  R[19:WELDING1 Z OFFS]=R[19:WELDING1 Z OFFS]+2    ;
 105:   ;
 106:  CALL PR(97,R[17:WELDING1 X OFFS],R[18:WELDING1 Y OFFS],R[19:WELDING1 Z OFFS]) ;
 107:  CALL CHECK_SIGN(97,2) ;
 108:   ;
 109:L PR[39:WELDING_1_REF] 100mm/sec FINE Offset,PR[97:OFFSET W1]    ;
 110:   ;
 111:  CALL GRP_OPN    ;
 112:   ;
 113:  DO[52:PICK AND PLACE PLS]=PULSE,1.0sec ;
 114:   ;
 115:  //CALL M1_CHUCK_CLS    ;
 116:   ;
 117:  R[115:W1 HANDLING]=R[40:Handling height]    ;
 118:  R[116:W1 PART HEIGHT]=R[41:WP Height]    ;
 119:  R[117:W1 X OFFS]=R[17:WELDING1 X OFFS]    ;
 120:  R[118:W1 Y OFFS]=R[18:WELDING1 Y OFFS]    ;
 121:   ;
 122:  R[40:Handling height]=0    ;
 123:  R[41:WP Height]=0    ;
 124:   ;
 125:  R[19:WELDING1 Z OFFS]=R[116:W1 PART HEIGHT]+R[65:W1 CHUCK DEPTH]+20    ;
 126:   ;
 127:  CALL PR(97,R[17:WELDING1 X OFFS],R[18:WELDING1 Y OFFS],R[19:WELDING1 Z OFFS]) ;
 128:   ;
 129:L PR[39:WELDING_1_REF] 100mm/sec FINE Offset,PR[97:OFFSET W1]    ;
 130:   ;
 131:  UFRAME_NUM=12 ;
 132:  UTOOL_NUM=1 ;
 133:   ;
 134:J PR[40:IN_WELDING_1] 100% CNT100    ;
 135:   ;
 136:J PR[38:V_WELDING_1 ] 100% CNT100    ;
 137:J PR[1:HOME] 100% FINE    ;
 138:   ;
 139:  END ;
/POS
/END
