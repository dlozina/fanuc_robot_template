/PROG  JOB3
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 2176;
CREATE		= DATE 19-03-21  TIME 15:04:30;
MODIFIED	= DATE 19-04-17  TIME 15:58:30;
FILE_NAME	= JOB1;
VERSION		= 0;
LINE_COUNT	= 135;
MEMORY_SIZE	= 2592;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/MN
   1:  !GRIJAC 1 NA STROJ 1 ;
   2:   ;
   3:  CALL CLR_SCR    ;
   4:  GO[1]=R[100:Job Number] ;
   5:   ;
   6:  CALL H1_STOP    ;
   7:   ;
   8:  UFRAME_NUM=12 ;
   9:  UTOOL_NUM=1 ;
  10:   ;
  11:J PR[38:V_WELDING_1 ] 50% CNT100    ;
  12:   ;
  13:J PR[4:V_H1H2] 50% CNT100    ;
  14:   ;
  15:J PR[30:V_HEATER_1 ] 50% CNT100    ;
  16:   ;
  17:  WAIT DI[13:PermissSpace6]=ON    ;
  18:   ;
  19:  UFRAME_NUM=6 ;
  20:  UTOOL_NUM=1 ;
  21:   ;
  22:  R[12:HEATER1 Z OFFS]=R[111:HEAT1 HEIGHT]+20    ;
  23:  CALL PR(98,R[109:HEAT1 X OFFS],0,R[12:HEATER1 Z OFFS]) ;
  24:   ;
  25:J PR[31:HEATER_1_REF] 20% CNT20 Offset,PR[98:OFFSET H1]    ;
  26:   ;
  27:  R[12:HEATER1 Z OFFS]=R[110:HEAT1 HANDLING]+4    ;
  28:   ;
  29:  CALL PR(98,R[10:HEATER1 X OFFS],0,R[12:HEATER1 Z OFFS]) ;
  30:   ;
  31:L PR[31:HEATER_1_REF] 100mm/sec FINE Offset,PR[98:OFFSET H1]    ;
  32:   ;
  33:  CALL GRP_CLS    ;
  34:   ;
  35:  DO[52:PICK AND PLACE PLS]=PULSE,1.0sec ;
  36:   ;
  37:  R[40:Handling height]=R[110:HEAT1 HANDLING]    ;
  38:  R[41:WP Height]=R[111:HEAT1 HEIGHT]    ;
  39:   ;
  40:  R[109:HEAT1 X OFFS]=0    ;
  41:  R[110:HEAT1 HANDLING]=0    ;
  42:  R[111:HEAT1 HEIGHT]=0    ;
  43:   ;
  44:  R[12:HEATER1 Z OFFS]=R[40:Handling height]+50    ;
  45:   ;
  46:  CALL PR(98,R[10:HEATER1 X OFFS],0,R[12:HEATER1 Z OFFS]) ;
  47:   ;
  48:L PR[31:HEATER_1_REF] 100mm/sec FINE Offset,PR[98:OFFSET H1]    ;
  49:   ;
  50:  UFRAME_NUM=12 ;
  51:  UTOOL_NUM=1 ;
  52:   ;
  53:J PR[30:V_HEATER_1 ] 30% CNT100    ;
  54:J PR[4:V_H1H2] 30% CNT100    ;
  55:   ;
  56:  !TO WELDING 1 ;
  57:  UFRAME_NUM=12 ;
  58:  UTOOL_NUM=1 ;
  59:   ;
  60:J PR[38:V_WELDING_1 ] 30% CNT20    ;
  61:  WAIT DI[12:PermissSpace5]=ON    ;
  62:   ;
  63:  UFRAME_NUM=12 ;
  64:  UTOOL_NUM=1 ;
  65:   ;
  66:J PR[40:IN_WELDING_1] 30% CNT20    ;
  67:   ;
  68:  CALL PR(97,0,0,(-250)) ;
  69:   ;
  70:J PR[40:IN_WELDING_1] 30% CNT20 Tool_Offset,PR[97:OFFSET W1]    ;
  71:   ;
  72:  UFRAME_NUM=5 ;
  73:  UTOOL_NUM=1 ;
  74:   ;
  75:  R[64:W1 CHUCK HEIGHT]=126-GI[24:M1_Chuck_Height]    ;
  76:  R[65:W1 CHUCK DEPTH]=GI[23:M1_Chuck_Depth]    ;
  77:   ;
  78:  R[17:WELDING1 X OFFS]=R[42:Pick TCP Offs]+14    ;
  79:   ;
  80:  !CORRECTION X OFFS ;
  81:  R[1:CHECK SIGN GI]=(GI[25:M1_Chuck_XOffs]-(GI[25:M1_Chuck_XOffs] DIV 32786)*65536) ;
  82:   ;
  83:  R[17:WELDING1 X OFFS]=R[17:WELDING1 X OFFS]+20+R[1:CHECK SIGN GI]    ;
  84:   ;
  85:  R[18:WELDING1 Y OFFS]=GI[26:M1_Chuck_YOffs ]    ;
  86:   ;
  87:  R[19:WELDING1 Z OFFS]=R[40:Handling height]+R[64:W1 CHUCK HEIGHT]+20    ;
  88:   ;
  89:  CALL PR(97,R[17:WELDING1 X OFFS],R[18:WELDING1 Y OFFS],R[19:WELDING1 Z OFFS]) ;
  90:  CALL CHECK_SIGN(97,2) ;
  91:   ;
  92:J PR[39:WELDING_1_REF] 30% FINE Offset,PR[97:OFFSET W1]    ;
  93:   ;
  94:  R[19:WELDING1 Z OFFS]=R[40:Handling height]-R[65:W1 CHUCK DEPTH]    ;
  95:  R[19:WELDING1 Z OFFS]=R[19:WELDING1 Z OFFS]+6    ;
  96:   ;
  97:  CALL PR(97,R[17:WELDING1 X OFFS],R[18:WELDING1 Y OFFS],R[19:WELDING1 Z OFFS]) ;
  98:  CALL CHECK_SIGN(97,2) ;
  99:   ;
 100:L PR[39:WELDING_1_REF] 100mm/sec FINE Offset,PR[97:OFFSET W1]    ;
 101:   ;
 102:  CALL GRP_OPN    ;
 103:   ;
 104:  DO[52:PICK AND PLACE PLS]=PULSE,1.0sec ;
 105:   ;
 106:  CALL M1_CHUCK_CLS    ;
 107:   ;
 108:  R[115:W1 HANDLING]=R[40:Handling height]    ;
 109:  R[116:W1 PART HEIGHT]=R[41:WP Height]    ;
 110:  R[117:W1 X OFFS]=R[17:WELDING1 X OFFS]    ;
 111:  R[118:W1 Y OFFS]=R[18:WELDING1 Y OFFS]    ;
 112:   ;
 113:  R[40:Handling height]=0    ;
 114:  R[41:WP Height]=0    ;
 115:   ;
 116:  R[19:WELDING1 Z OFFS]=R[116:W1 PART HEIGHT]+R[65:W1 CHUCK DEPTH]+20    ;
 117:   ;
 118:  CALL PR(97,R[17:WELDING1 X OFFS],R[18:WELDING1 Y OFFS],R[19:WELDING1 Z OFFS]) ;
 119:   ;
 120:L PR[39:WELDING_1_REF] 100mm/sec FINE Offset,PR[97:OFFSET W1]    ;
 121:   ;
 122:  UFRAME_NUM=12 ;
 123:  UTOOL_NUM=1 ;
 124:   ;
 125:J PR[40:IN_WELDING_1] 50% CNT100    ;
 126:   ;
 127:J PR[38:V_WELDING_1 ] 50% CNT100    ;
 128:   ;
 129:  CALL M1_DOOR_CLS    ;
 130:   ;
 131:  CALL M1_NC_START    ;
 132:   ;
 133:J PR[1:HOME] 50% FINE    ;
 134:   ;
 135:  END ;
/POS
/END
